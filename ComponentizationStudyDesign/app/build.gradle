plugins {
    id 'com.android.application'
}

// 赋值与引用
def androidId = rootProject.ext.androidId
def appId = rootProject.ext.appId
def support = rootProject.ext.dependencies

android {
    compileSdkVersion androidId.compileSdkVersion
    buildToolsVersion androidId.buildToolsVersion

    defaultConfig {
        applicationId appId.applicationId
        minSdkVersion androidId.minSdkVersion
        targetSdkVersion androidId.targetSdkVersion
        versionCode androidId.versionCode
        versionName androidId.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 用isRelease变量，标识当前整个项目是集成化模式还是组件化模式
        buildConfigField("boolean", "isRelease", String.valueOf(isRelease))
    }

    buildTypes {
        debug{
            buildConfigField("String", "url", "\"${url.debug}\"");
        }
        release {
            minifyEnabled false
            buildConfigField("String", "url", "\"${url.release}\"");
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // 标准写法
    //implementation group:"androidx.appcompat", name:"appcompat", version:"1.1.0"

    // 简写
    //implementation 'androidx.appcompat:appcompat:1.1.0'

    // 最简洁的方法
    support.each {k, v->implementation v}

    // 公共基础库
    implementation project(":common")

    // 如果是发布版本，说明是集成化模式，各个模块不能独立运行了
    if(isRelease){
        implementation project(":order")
        implementation project(":personal")
    }
}